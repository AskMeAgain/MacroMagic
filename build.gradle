plugins {
    id 'org.jetbrains.intellij' version '1.5.0'
    id 'java'
    id 'checkstyle'
}

group 'io.github.askmeagain'
version System.getenv("RELEASE_VERSION")

repositories {
    mavenCentral()
}

dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junit_version}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junit_version}"

    compileOnly "org.projectlombok:lombok:${lombok_version}"
    annotationProcessor "org.projectlombok:lombok:${lombok_version}"

    implementation "ch.qos.logback:logback-classic:${logback_classic_version}"

    implementation "com.fasterxml.jackson.core:jackson-databind:2.13.0"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.13.0"

}


intellij {
    version = '2021.3.1'
    plugins = ['com.intellij.java']
}

patchPluginXml {
    version = project.getVersion()
    changeNotes = new File("marketplace-changenotes.html").text
    pluginDescription = new File("marketplace-description.html").text
}

test {
    useJUnitPlatform()
}

signPlugin {
    certificateChain = System.getenv("CERTIFICATE_CHAIN")
    privateKey = System.getenv("PRIVATE_KEY")
    password = System.getenv("PRIVATE_KEY_PASSWORD")
}

publishPlugin {
    token = System.getenv("PUBLISH_TOKEN")
    channels = ['unstable']
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

checkstyle {
    ignoreFailures = false
    maxWarnings = 0
    maxErrors = 0
    toolVersion '9.1'
    configFile = rootProject.file('checkstyle.xml')
}
